buildscript {
    ext {
    }
    repositories {
        mavenCentral()
    }
    dependencies {
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "jacoco"

jar {
    baseName = '01-endpoints'
    version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java.srcDir file('src/main/java')
        resources.srcDir file('src/main/resources')
    }
    assessment {
        java.srcDir file('assessment/test')
        resources.srcDir file('assessment/resources')
    }
}

task assess(type: Test) {
    testClassesDir = sourceSets.assessment.output.classesDir
    classpath = sourceSets.assessment.runtimeClasspath
}

ext {
    limits = [
            'instruction': 95,
            'branch'     : 90,
            'line'       : 95,
            'complexity' : 85,
            'method'     : 90,
            'class'      : 95
    ]
}

jacocoTestReport {
    reports {
        xml.enabled true
    }

    // taken from https://github.com/springfox/springfox/blob/master/gradle/coverage.gradle
    // TODO: When [this PR](https://github.com/gradle/gradle/issues/824) gets released
    // we can get automated coverage from the jacoco plugin
    doLast {
        def report = file("${jacoco.reportsDir}/test/jacocoTestReport.xml")
        def htmlReport = file("${jacoco.reportsDir}/test/html/index.html")
        logger.lifecycle("Checking coverage results: ${report}")
        logger.lifecycle("Html report: ${htmlReport}")

        def parser = new XmlParser()
        parser.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false);
        parser.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false)
        def results = parser.parse(report)

        def percentage = {
            if (null == it) {
                //if it does not exist assume 100%
                return 100
            }
            def covered = it.'@covered' as Double
            def missed = it.'@missed' as Double
            ((covered / (covered + missed)) * 100).round(2)
        }

        def counters = results.counter
        def metrics = [:]
        metrics << [
                'instruction': percentage(counters.find { it.'@type'.equals('INSTRUCTION') }),
                'branch'     : percentage(counters.find { it.'@type'.equals('BRANCH') }),
                'line'       : percentage(counters.find { it.'@type'.equals('LINE') }),
                'complexity' : percentage(counters.find { it.'@type'.equals('COMPLEXITY') }),
                'method'     : percentage(counters.find { it.'@type'.equals('METHOD') }),
                'class'      : percentage(counters.find { it.'@type'.equals('CLASS') })
        ]


        def failures = []
        def canIncrease = []
        metrics.each {
            def limit = limits[it.key]
            if (it.value < limit) {
                failures.add("- ${it.key} coverage rate is: ${it.value}%, minimum is ${limit}%")
            }
            if (it.value > limit + 1) {
                canIncrease.add("- ${it.key} coverage rate is: ${it.value}%, minimum is ${limit}%")
            }
        }

        if (failures) {
            logger.quiet("------------------ Code Coverage Failed -----------------------")
            failures.each {
                logger.quiet(it)
            }
            logger.quiet("---------------------------------------------------------------")
            if (!project.hasProperty("skipCoverage")) {
                throw new GradleException("Code coverage failed")
            }
        }
        if (canIncrease) {
            logger.quiet("------------------ Code Coverage Improved! -----------------------")
            canIncrease.each {
                logger.quiet(it)
            }
            logger.quiet("---------------------------------------------------------------")
        }
    }

}


dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.10'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    testCompile 'junit:junit:4.12'

    assessmentCompile sourceSets.main.output
    assessmentCompile configurations.testCompile
    assessmentCompile sourceSets.test.output

    assessmentRuntime configurations.testRuntime
}